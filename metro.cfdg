startshape metro

rule metro{
    random_walk{ b 0.9 sat 1 hue 0 }       /* marunouchi */
    random_walk{ b 0.7 sat 1 hue 30 }      /* yurakucho  */
    random_walk{ b 0.3 sat 1 hue 140 }     /* chiyoda    */
    random_walk{ b 0.7 sat 1 hue 300 }     /* hanzomon   */
    random_walk{ h 191.14 sat 1 b 0.6988 } /* tozai      */
    random_walk{ h 26.05 sat 1 b 0.9157 }  /* ginza      */
    random_walk{ b 0.5843 }                /* hibiya     */

}

rule random_walk{ random_walk{ r 60 } }
rule random_walk{ random_walk{ y 3 } }
rule random_walk 0.1 { start_line{} }

rule start_line{
    station{}
    rail{}
}

rule station{
    CIRCLE{ s 1.5  z 1  }
    CIRCLE{ s 1 b 1 sat -1 z 2 }
}

/* go */
rule rail 40 {
    CIRCLE{ b -1 z 0 b 0.6 }
    rail{ x 0.1 } 
}

/* station */
rule rail 0.3 { station{} rail{ x 0.1 } }

/* turn */
rule rail 0.04 {    rail{ r 90 } }
rule rail 0.04 {    rail{ r -90 } }
rule rail 0.1 {    rail{ r 30 } }
rule rail 0.1 {    rail{ r 60 } }
rule rail 0.1 {    rail{ r -30 } }
rule rail 0.1 {    rail{ r -60 } }

/* end */
rule rail 0.05{ station{} }

